// Glowing sine waves
// See https://www.shadertoy.com/view/3s3GDn for comments on the glow

float time;

float getWaveGlow(vec2 pos, float radius, float intensity, float speed,
                  float amplitude, float frequency, float shift){
    
	float dist = abs(pos.y + amplitude * sin(shift + speed * time + pos.x * frequency));
    dist = 1.0/dist;
    dist *= radius;
    dist = pow(dist, intensity);
    return dist;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord ){
    
    vec2 uv = fragCoord/iResolution.xy;
    float widthHeightRatio = iResolution.x/iResolution.y;
    vec2 centre = vec2(0.5, 0.5);
    vec2 pos = centre - uv;
    pos.y /= widthHeightRatio;
    
    time = iTime * 0.4;
    
    float intensity = 1.5;
    float radius = 0.02;
    
    vec3 col = vec3(0.0);
    float dist = 0.0;
    
    //Use time varying colours from the basic template
	//Add it to vec3(0.1) to always have a bright core
    dist = getWaveGlow(pos, radius,intensity, 2.0, 0.018, 3.7, 0.0);
	col += dist * (vec3(0.1) + 0.5 + 0.5*cos(3.14+time+vec3(0,2,4)));

 	dist = getWaveGlow(pos, radius, intensity, 4.0, 0.018, 6.0, 2.0);
	col += dist * (vec3(0.1) + 0.5 + 0.5*cos(1.57+time+vec3(0,2,4)));

	dist = getWaveGlow(pos, radius*0.5, intensity, -5.0, 0.018, 4.0, 1.0);
	col += dist * (vec3(0.1) + 0.5 + 0.5*cos(time+vec3(0,2,4)));
    
    //See comments in https://www.shadertoy.com/view/3s3GDn
    col = 1.0 - exp(-col);
    
    //Gamma
   // col = pow(col, vec3(0.4545));
    
    // Output to screen
    fragColor = vec4(col, 1.0);
}